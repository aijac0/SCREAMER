Water Line Calculations
!
TIME-STEP          0.1E-9
RESOLUTION-TIME    1.0E-9
END-TIME           1E-6
NUMBER-PRINTS      5
EXECUTE-CYCLES     ALL
Grids              no
ECHO               NO	
MAX-POINTS         3001
!
!
!   Rick Spielman
!
!  A water line driver for radiographic applications.
!
!  MARX: A fast Marx with 32 stages using the GA FMG cap
!     32 x 1.5 µF
!
!  IS: A 3 Ohm IS, 60 ns long
!
!  PFRP diode: Resistive look up table
!
!
BRANCH
RCG     1E12    46.9E-9
INITIAL   VC1    3E6
UFO VC1
$V_MARX
UFO EC1
$E_Marx
!
!    Marx inductance and ESR
!    32 x the single capacitor value
!    20 nH and 50 mOhm
!
RLS  0.640  640E-9
!
!   Marx switch resistance and inductance
!   16 x 10 mOhm, 16 x 10 nH
!
RLS  0.16  160E-9
UFO IR2
$I_Marx
!
!    The  parallel Marx resistance
!
RCG  100.0  0
UFO VR1
$V_Marx_out
!
!     Marx connection inductance
!
RLS  0  0
!
!  **************** INTERMEDIATE STORE **************
!                      3 Ohm, 60 ns long
!
Trline Lin 60e-9 3
UFO Vout
$V_IS_out
UFO Eout
$E_IS_out
!
!  **************** IS Gas Switch ***********************
!
RLS 1e12 50e-9
Variable R2 Decay
1e6 0.1 245e-9 1e-9
UFO IR2
$I_Switch
!
! Output Water line
!
Trline Lin 100e-9 3
!
! Load Resistance
!
RCG 3.0 0
UFO IR1
$I_Out
UFO VR1
$V_Out
UFO ER1
$E_Out
!